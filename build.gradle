buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }

    dependencies {
        classpath group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.3.6'
        classpath group: 'com.github.dynamicextensionsalfresco', name: 'gradle-plugin', version: '1.1.0'
        classpath group: 'com.parashift.amp', name: 'amp-plugin', version: '1.0-SNAPSHOT'
    }
}

apply plugin: 'alfresco-dynamic-extension'
apply plugin: 'alfresco-amp'

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

version = "git describe --tags".execute().text.trim()

ext {
    alfresco = [:]
    alfresco.version = "5.0.1"
}

repositories {
    mavenCentral()
    maven { url "https://maven.alfresco.com/nexus/content/groups/public/" }

    maven {
        credentials {
            username project['alfresco.repo.private.username']
            password project['alfresco.repo.private.password']
        }

        url "https://maven.alfresco.com/nexus/content/groups/private/"
    }
    mavenLocal()
    flatDir name: 'localRepository', dirs: 'lib'
}

configurations {
    all*.exclude group: 'maven-plugins'
    all*.exclude group: 'org.jvnet.staxex'
}

dependencies {
    compile (group: "org.alfresco", name: "alfresco", version: "${alfresco.version}", classifier: "classes")
    compile 'org.slf4j:slf4j-api:1.7.5'
}

jar {
    manifest {
        instructionReplace 'Bundle-Name', project.title
        instruction 'Bundle-Description', project.description
        instruction 'Alfresco-Spring-Configuration', 'com.parashift'
        instruction 'Bundle-DocURL', 'https://github.com/lfridael/example-dynamic-extension'
    }
}

amp {
    id = "com.parashift." + project.name
    baseName = project.name
    alias = project.name
    version = project.version
    title = project.title
    description = project.description
    dependsOn("com.github.dynamicextensionsalfresco:1.0:2.0")
    addLibDir("ampLib")
}

alfrescoDynamicExtensions {
    repository {
        endpoint {
            host = 'localhost'
            port = '8080'
            serviceUrl = '/alfresco/service'
        }
        authentication {
            username = "admin"
            password = "admin"
        }
    }
}
